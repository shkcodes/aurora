plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'io.gitlab.arturbosch.detekt'
    id 'androidx.navigation.safeargs.kotlin'
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdkVersion Versions.compileSdkVersion

    defaultConfig {
        applicationId "com.shkcodes.aurora"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }

    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes.each { type ->
        type.buildConfigField 'String', 'CONSUMER_KEY', "\"" + localProperties['consumer_key'] + "\""
        type.buildConfigField 'String', 'CONSUMER_SECRET', "\"" + localProperties['consumer_secret'] + "\""
        type.buildConfigField 'String', 'ACCESS_TOKEN', "\"" + localProperties['access_token'] + "\""
        type.buildConfigField 'String', 'ACCESS_SECRET', "\"" + localProperties['access_secret'] + "\""
        type.buildConfigField 'String', 'CALLBACK_URL', "\"" + localProperties['callback_url'] + "\""
        type.buildConfigField 'String', 'GIPHY_KEY', "\"" + localProperties['giphy_key'] + "\""
    }
}
detekt {
    autoCorrect = true
}

dependencies {

    implementation Dependencies.coreKtx
    implementation Dependencies.appCompat

    implementation Dependencies.navigation
    implementation Dependencies.navigationUi
    implementation Dependencies.material
    implementation Dependencies.lifecycle
    kapt Dependencies.lifecycleCompiler

    implementation Dependencies.viewModel

    implementation Dependencies.hilt
    implementation Dependencies.hiltViewModel
    kapt Dependencies.hiltCompiler
    kapt Dependencies.hiltCompilerJetpack

    implementation Dependencies.coroutinesCore
    implementation Dependencies.coroutinesAndroid

    implementation Dependencies.reclaim

    implementation Dependencies.coil
    implementation Dependencies.coilGifSupport
    implementation Dependencies.swipeRefresh
    implementation Dependencies.viewPager
    implementation Dependencies.constraintLayout

    detektPlugins Dependencies.detektFormatter

    implementation Dependencies.retrofit
    implementation Dependencies.okhttp
    implementation Dependencies.moshi
    implementation Dependencies.moshiConverter
    implementation Dependencies.scalarsConverter
    kapt Dependencies.moshiCompiler

    coreLibraryDesugaring Dependencies.desugarLib

    implementation Dependencies.room
    implementation Dependencies.roomKtx
    kapt Dependencies.roomCompiler

    implementation Dependencies.jsoup

    implementation Dependencies.twitter4j

    implementation Dependencies.giphy

    implementation Dependencies.timber

    implementation Dependencies.gestureView
    implementation Dependencies.exoplayer

    testImplementation TestDependencies.junit
    testImplementation TestDependencies.coroutines
    testImplementation TestDependencies.mockk
    testImplementation TestDependencies.mockkJvmAgent
    testImplementation TestDependencies.turbine
}